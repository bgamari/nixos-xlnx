{ config, pkgs, lib, modulesPath, ... }:

let
  boot-bin = pkgs.boot-bin-xlnx.override {
    inherit (config.boot-bin-xlnx) bitstream fsbl pmufw dtb;
  };
in

{
  options.boot-bin-xlnx = {
    bitstream = lib.mkOption {
      type = lib.types.path;
      example = lib.literalExpression "./firmware/system.bit";
      description = lib.mdDoc ''
        Path to system.bit generated by Vitis.
        Can also be generated using ./xsa2fw <vivado_exported.xsa>
      '';
    };
    fsbl = lib.mkOption {
      type = lib.types.path;
      example = lib.literalExpression "./firmware/zynqmp_fsbl.elf";
      description = lib.mdDoc ''
        Path to zynqmp_fsbl.elf generated by Vitis.
        Can also be generated using ./xsa2fw <vivado_exported.xsa>
      '';
    };
    pmufw = lib.mkOption {
      type = lib.types.path;
      example = lib.literalExpression "./firmware/pmufw.elf";
      description = lib.mdDoc ''
        Path to pmufw.elf generated by Vitis.
        Can also be generated using ./xsa2fw <vivado_exported.xsa>
      '';
    };
    dtb = lib.mkOption {
      type = lib.types.path;
      example = lib.literalExpression "./firmware/dtb/system.dtb";
      description = lib.mdDoc ''
        Path to system.dtb generated by Vitis.
        Can also be generated using ./xsa2fw <vivado_exported.xsa>
      '';
    };
  };

  imports = [
    "${modulesPath}/profiles/base.nix"
    "${modulesPath}/installer/sd-card/sd-image.nix"
    ./nixos.nix
  ];
  disabledModules = [ "${modulesPath}/profiles/all-hardware.nix" ];

  config = {
    sdImage = {
      # Depending on the FSBL setup, BOOT.BIN can be quite large
      firmwareSize = 100;
      populateFirmwareCommands = ''
        cp ${boot-bin}/BOOT.BIN firmware/
      '';
      populateRootCommands = ''
        mkdir -p ./files/boot
        ${config.boot.loader.generic-extlinux-compatible.populateCmd} -c ${config.system.build.toplevel} -d ./files/boot
      '';
    };

    environment.systemPackages = [
      (pkgs.writeShellApplication {
        name = "xlnx-firmware-update";
        text = ''
          cp ${boot-bin}/BOOT.BIN /boot/firmware/
        '';
      })
    ];

    # Some modules aren't available...
    boot.initrd.includeDefaultModules = false;
    boot.initrd.availableKernelModules = [
      "ahci"
      "sata_inic162x"
      "sata_sil24"
      # NVMe
      "nvme"
      # Standard SCSI stuff.
      "sr_mod"
      # Support USB keyboards, in case the boot fails and we only have
      # a USB keyboard, or for LUKS passphrase prompt.
      "uhci_hcd"
      "ehci_hcd"
      "ehci_pci"
      "ohci_hcd"
      "ohci_pci"
      "xhci_hcd"
      "xhci_pci"
      "usbhid"
      "hid_generic" "hid_lenovo" "hid_apple" "hid_roccat"
      "hid_logitech_hidpp" "hid_logitech_dj" "hid_microsoft" "hid_cherry"
      # Broadcom
      "vc4"
    ];
  };
}
